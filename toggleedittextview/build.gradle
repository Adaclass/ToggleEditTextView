apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

def VERSION = '0.1.0'

group = 'com.camerash'
version = VERSION

def bintrayUser = System.getenv('BINTRAY_USER')
def bintrayApiKey = System.getenv('BINTRAY_API_KEY')

if (project.rootProject.file('local.properties').exists()) {
    Properties localProperties = new Properties()
    localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

    bintrayUser = localProperties.getProperty('bintray.user', bintrayUser)
    bintrayApiKey = localProperties.getProperty('bintray.apikey', bintrayApiKey)
}

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1
        versionName VERSION

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation "com.android.support:design:27.1.1"
}

repositories {
    mavenCentral()
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    excludes = ['**/*.kt'] // exclude kotlin files
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = bintrayUser
    key = bintrayApiKey
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'ToggleEditTextView'
        desc = 'Easily switch between EditText and TextView seamlessly.'
        websiteUrl = 'https://github.com/Camerash/ToggleEditTextView'
        vcsUrl = 'https://github.com/Camerash/ToggleEditTextView.git'
        licenses = ['MIT']
        publish = true
        publicDownloadNumbers = true
        version {
            name = VERSION
            desc = "ToggleEditTextView ${VERSION}"
            released  = new Date()
            vcsTag = VERSION
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                groupId this.group
                artifactId 'toggleedittextview'

                name 'toggleedittextview'
                description 'Easily switch between EditText and TextView seamlessly.'
                url 'https://github.com/Camerash/ToggleEditTextView'

                version = this.version

                licenses {
                    license {
                        name 'The MIT License'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id 'camerash'
                        name 'Esmond Wong'
                        email 'esmond9981127@gmail.com'
                    }
                }
                scm {
                    connection 'https://github.com/Camerash/ToggleEditTextView.git'
                    developerConnection 'https://github.com/Camerash/ToggleEditTextView.git'
                    url 'https://github.com/Camerash/ToggleEditTextView'
                }
            }
        }
    }
}